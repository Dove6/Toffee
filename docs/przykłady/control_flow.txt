pull std.io;

init const one = 1, const two = 2, const three = 3, i, j;;;;;;;;;;  // postawienie kilku średników nie jest błędem

if (one + two == three) {
    print("Matematyka działa");
} elif (one + two != three) {  // elify są opcjonalne
    print("Matematyka nie działa");
} else {  // else też są opcjonalne
    print("Natrafiono na niepokojący błąd w interpreterze...")
};

if (true) {
    print("Zawsze zadziała");
};
if (true) print("Zawsze zadziała");  // to samo, ale w formie wyrażenia
// możliwe jest użycie również else oraz elseif w wyrażeniu
if (i != null) print("i nie jest nullem") else print("i jest nullem");
// przypisanie wyniku if
i = if (two < three) 5 else 8;
i = (if (two < three) 5) + 10;  // brakuje else - w przypadku niespełnienia warunku zostanie zwrócona wartość null
//  ^------------------^------ wymagane nawiasy, aby "+ 10" nie zostało "podłączone" do "5"

// każdy blok zwraca wartość ostatniej instrukcji, jeśli średnik po niej został pominięty
// (i jeśli istnieje jakaś sensowna wartość do zwrócenia, w przeciwnym wypadku zwracany jest null)
i = {
    one + two;
    print("Ten tekst zostanie wypisany.");  // nie istnieją funkcje typu void, można jednak zwrócić null
    j = 5  // wyrażenie przypisania
};
// w tym miejscu obie zmienne (i oraz j) mają wartość 5

// poniżej zaprezentowana jest pętla for z opcjonalną deklaracją licznika
// podobnie jak if/elif/else, również pętlę for można zapisać w formie skróconej
// parametrem pętli jest opcjonalnie nazwa zmiennej, a obowiązkowo zakres, po którym dokonuje się iteracji
// zakres może mieć trzy formy ze względu na opcjonalną wartość początkową (domyślnie 0) oraz krok (domyślnie 1)
// wartość początkowa jest podawana włącznie, a końcowa nie
for (5) {
    print("i");  // wypisanie pięć razy wartości "i"
};
for (i, 0:5) print(i);  // pętla wypisuje w kolejnych liniach cyfry 0, 1, 2, 3, i 4
//   ^ licznik przykrywa deklarację i z przestrzeni globalnej
for (outer, 4:-1:-1) {
    outer = 5;  // błąd - licznik jest stałą w danej iteracji
    for (inner, 3) {
        print("Liczniki:");
        print(outer .. "/" .. inner);
    };
};
// pętla for potraktowana jako wyrażenie zwraca wartość licznika w momencie jej zakończenia (nawet, jeśli nie został on zadeklarowany)
// może to być przydatne w przypadku wymuszonego zatrzymania pętli (break)
j = for (5) {
    print("iteration");
};
// j ma teraz wartość 4

// pętla while
while (true) {
    print("Ten tekst zostanie wypisany jednokrotnie.")
    break;
};
// działa "zwyczajnie", w roli wyrażenia zwraca wartość sprawdzanego warunku

// instrukcja break istnieje w dwóch wersjach: zwykłej (break) oraz warunkowej (break_if)
while (true) {
    i = null;
    break;
    j = 5;
    break_if(true);
};
// i ma teraz wartość null, j bez zmian (przypisanie nie zostało wykonane ze względu na przerwanie pętli)
for (digit, 10) {
    print(digit);
    break_if(digit >= 5);
};
// licznik zostanie wypisany tylko 5 razy
